# When OS type is Linux:
# This file is sourced only for interactive shells.
# It should contain commands to set up aliases, fun
# -ctions, options, key bindings, etc:
# See /usr/share/zsh/help

# Compile '.zshrc' to speed up loading. {{{
if [ ! -f $ZDOTDIR/.zshrc.zwc -o $(readlink $ZDOTDIR/.zshrc.linux) -nt $ZDOTDIR/.zshrc.zwc ]; then
    zcompile $ZDOTDIR/.zshrc.zwc $ZDOTDIR/.zshrc.linux
fi
#}}}

# Build-in Function Settings {{{
# TODO: adjustment
# + Autoload {{{
autoload -Uz colors && colors
autoload -Uz is-at-least
if is-at-least 4.3.10; then
    autoload -Uz vcs_info
    autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
fi

# Can execute man with the Esc-h
if (( ${+aliases[run-help]} )); then
    unalias run-help
fi
autoload -Uz run-help
autoload -Uz run-help-git
autoload -Uz run-help-svn
# + }}}

# TODO: English commentary
# + Options {{{
# ++ Changing Directories {{{
# Omit cd
setopt auto_cd

# Add cd directory to directories stack
# For example, 'cd - [tab]' allows you to jump to a
# directories in the past
setopt auto_pushd

# Omit '~'
setopt cdable_vars

# Donâ€™t puhf the same directory onto th directories stack
setopt pushd_ignore_dups
# ++ }}}

# ++ Completion {{{
# Don't change the position of prompt when completing
setopt always_last_prompt

# Move the cursor to the end after complete completion
setopt always_to_end

# è£œå®Œå€™è£œãŒè¤‡æ•°ã‚ã‚‹ã¨ãä¸€è¦§è¡¨ç¤º
setopt auto_list

# tabã§è£œå®Œå€™è£œã‚’åˆ‡ã‚Šæ›¿ãˆã‚‹
setopt auto_menu

# å¤‰æ•°å, æ‹¬å¼§ã®å¯¾å¿œã‚’è£œå®Œã™ã‚‹
setopt auto_param_keys

# ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’è£œå®Œã™ã‚‹ã¨æœ€å¾Œã«ã‚¹ãƒ©ãƒƒã‚·ãƒ¥ã‚’è¿½åŠ ã™ã‚‹
setopt auto_param_slash

# ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã®è£œå®Œã§è¿½åŠ ã—ãŸã‚¹ãƒ©ãƒƒã‚·ãƒ¥ã‚’å‰Šé™¤ã™ã‚‹
setopt auto_remove_slash

# aliasã‚’å±•é–‹ã—ã¦è£œå®Œ
setopt complete_aliases

# èªžã®é€”ä¸­ã§ã‚‚ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã§è£œå®Œ
setopt complete_in_word

# tabè£œå®ŒãŒæˆåŠŸã—ãªã„æ™‚ãƒ“ãƒ¼ãƒ—ã§é€šçŸ¥ã—ãªã„
setopt list_beep

# è£œå®Œå€™è£œã‚’è©°ã‚ã¦è¡¨ç¤º
setopt list_packed

# è£œå®Œå€™è£œä¸€è¦§ã§ãƒ•ã‚¡ã‚¤ãƒ«ã®ç¨®åˆ¥ã‚’ãƒžãƒ¼ã‚¯è¡¨ç¤º
setopt list_types

# ç¶šããŒã‚ã‚‹å ´åˆ, ã¾ã æ±ºå®šã—ãªã„
setopt rec_exact
# ++ }}}

# ++ Expansion and Globbing {{{
# For example, Can make folders 1, 2, 3 by 'mkdir {1-3}'
setopt brace_ccl

# Use additional characters for globs
setopt extended_glob

# Do not require a leading â€˜.â€™ in a filename to be matched explicit
setopt glob_dots

# sã‚„&ã®å±•é–‹ä¿®é£¾ãƒžãƒƒãƒãƒ³ã‚°ã‚’ãƒ‘ã‚¿ãƒ¼ãƒ³ãƒžãƒƒãƒã«ã™ã‚‹
setopt hist_subst_pattern

# ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã® = ä»¥é™ã®è£œå®Œã‚’å¯èƒ½ã«ã™ã‚‹
setopt magic_equal_subst

# If directory matches by expanding file name, appends / to the end
setopt mark_dirs

# Suppress glob warning
unsetopt nomatch

# å±•é–‹ã«å«ã¾ã‚Œã‚‹é…åˆ—ã‚’åˆ¥ã€…ã«å±•é–‹ã™ã‚‹
setopt rc_expand_param
# ++ }}}

# ++ History {{{
# æ™‚åˆ»ã‚’è¨˜éŒ²
setopt extended_history

# if conflict occurred, delete the old one.
setopt hist_ignore_all_dups

# Erase extra blanks
setopt hist_ignore_dups

# If there is a space at the beginning, don't record history
setopt hist_ignore_space

# historyã‚³ãƒžãƒ³ãƒ‰ã¯å±¥æ­´ã«ç™»éŒ²ã—ãªã„
setopt hist_no_store

# Stuff the extra spaces and record
setopt hist_reduce_blanks

# åŒæ™‚ã«èµ·å‹•ã—ãŸzshã®é–“ã§ãƒ’ã‚¹ãƒˆãƒªã‚’å…±æœ‰ã—ãªã„
unsetopt share_history
# ++ }}}

# ++ Input/Output {{{
# ã‚¨ã‚¤ãƒªã‚¢ã‚¹ã‚’æ‹¡å¼µã™ã‚‹
setopt aliases

# Don't overwrite with redirect
setopt clobber

# ã‚¹ãƒšãƒ«ãƒŸã‚¹ã®ä¿®æ­£ã‚’ã™ã‚‹
setopt correct

# ã‚³ãƒžãƒ³ãƒ‰è¡Œã®å…¨ã¦ã®å¼•æ•°ã«å¯¾ã—ã¦ã‚¹ãƒšãƒ«ãƒŸã‚¹ã®ä¿®æ­£ã‚’è¡Œã†
setopt correct_all

# Don't use 'C-q' and 'C-s'
setopt no_flow_control

# EOFã‚’èª­ã¿è¾¼ã‚“ã§ã‚‚çµ‚äº†ã—ãªã„
setopt ignore_eof

# ãƒ¡ãƒ¼ãƒ«ãƒ•ã‚¡ã‚¤ãƒ«ã«ã‚¢ã‚¯ã‚»ã‚¹ãŒã‚ã‚‹ã¨è­¦å‘Šã‚’ç™ºã™ã‚‹
setopt mail_warning

# ã‚³ãƒžãƒ³ãƒ‰åã«/ãŒå«ã¾ã‚Œã¦ã„ã¦ã‚‚ãã‚Œã‚’å«ã‚ãŸãƒ‘ã‚¹ã®æŽ¢ç´¢ã‚’è¡Œã†
setopt path_dirs

# Japanese file name can be displayed
setopt print_eight_bit

# 'rm *'ã®å®Ÿè¡Œå‰ã«10ç§’é–“ã®çŒ¶äºˆã‚’ä¸Žãˆã‚‹
set -o RM_STAR_WAIT
# ++ }}}

# ++ Job Control {{{
# If you enter a command with the same name as the suspended process, execute it
setopt auto_resume

# List jobs in the long format by default
setopt long_list_jobs
# ++ }}}

# ++ Prompting {{{
# Even if there isn't line feed code at the end of the output string, it is displayed
unsetopt prompt_cr

# Evaluate PROMPT variable expansion, command substitution, arithmetic operation etc for each display
setopt prompt_subst

# Delete the right prompt after executing the command
setopt transient_rprompt
# ++ }}}

# ++ Scripts and Functions {{{
# Handle to multiple redirects and pipes.
setopt multios
# ++ }}}

# ++ Zsh Line Editor {{{
# Don't sound a beep
unsetopt beep

# Emacs like key bind
setopt emacs

# Use ZLE
setopt zle
# ++ }}}
# + }}}

# TODO: adjustment
# + Typeset {{{
# An array to note missing features to ease diagnosis in case of problems
typeset -ga debian_missing_features

# Avoid duplication of $PATH
typeset -U path PATH
# + }}}

# TODO: adjustment
# + Zstyle (The zsh/zutil Module Settings) {{{
# ++ Prompt Settings {{{
# +++ PROMPT Setting {{{
function left-prompt {
    local first='025m%}'        # Text color of the user and host name 
    local first_b='254m%}'      # Background color of the user and host name
    local second='007m%}'       # Text color of the current directory
    local second_b='238m%}'     # Background color of the current directory
    local sharp='\ue0b0'        # Triangle
    local fg='%{[38;5;'
    local bg='%{[30;48;5;'
    local reset='%{[0m%}'
    local user_and_host="${bg}${first_b}${fg}${first}"
    local dir="${bg}${second_b}${fg}${second}"

    echo "${user_and_host}%n@%m${bg}${second_b}${fg}${first_b}${sharp} ${dir}%~${reset}${fg}${second_b}${sharp} ${reset}"
    return 0
}
PROMPT='`left-prompt`'
# +++ }}}

PROMPT2="%{$fg[green]%}%_> %{$reset_color%}"
SPROMPT="%{$fg[red]%}correct: %R -> %r [nyae]? %{$reset_color%}"

# +++ RPROMPT Setting {{{
function branch-mark {
    echo '\ue0a0^ '
    return 0
}
if is-at-least 4.3.10; then
    # ++++ Use vcs_info {{{
    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:*' formats "%F{green}%c%u$(branch-mark)[%b]%f"
    if is-at-least 4.3.10; then
        zstyle ':vcs_info:git:*' check-for-changes true
        zstyle ':vcs_info:git:*' stagedstr "%F{yellow}"
        zstyle ':vcs_info:git:*' unstagedstr "%F{red}"
        zstyle ':vcs_info:git:*' actionformats '%F{magenta}[%b|%a]%f'
    fi
    precmd() { vcs_info }
    RPROMPT='${vcs_info_msg_0_}'
    # ++++ }}}
else
    # ++++ Don't use vcs_info {{{
    function get-branch-status {
        local res color
        output=`git status --short 2> /dev/null`
        if [ -z "$output" ]; then
            res=':' # status Clean
            color='%{'${fg[green]}'%}'
        elif [[ $output =~ "[\n]?\?\? " ]]; then
            res='?:' # Untracked
            color='%{'${fg[yellow]}'%}'
        elif [[ $output =~ "[\n]? M " ]]; then
            res='M:' # Modified
            color='%{'${fg[red]}'%}'
        else
            res='A:' # Added to commit
            color='%{'${fg[cyan]}'%}'
        fi
        # echo ${color}${res}'%{'${reset_color}'%}'
        echo ${color}
        return 0
    }
    function get-branch-name {
        echo `git rev-parse --abbrev-ref HEAD 2> /dev/null`
        return 0
    }
    function branch-status-check {
        if [[ "$PWD" =~ '/\.git(/.*)?$' ]]; then
            return 0
        fi
        local branchname=`get-branch-name`
        if [[ -z $branchname ]]; then
            return 0
        fi
        local prefix=`get-branch-status`
        local suffix='%{'${reset_color}'%}'
        echo ${prefix}$(branch-mark)${branchname}${suffix}
        return 0
    }
    RPROMPT=$'`branch-status-check`'
    # ++++ }}}
fi
# +++ }}}
# ++ }}}

# ++ 'cdr' settings (Required before 'zplug load') {{{
if [ ! -d ${XDG_CACHE_HOME:-$HOME/.cache}/shell ]; then
    mkdir -p "${XDG_CACHE_HOME:-$HOME/.cache}/shell"
fi
if is-at-least 4.3.10; then
    add-zsh-hook chpwd chpwd_recent_dirs
    zstyle ':chpwd:*' recent-dirs-max 500
    zstyle ":chpwd:*" recent-dirs-default true
    zstyle ':chpwd:*' recent-dirs-file "${XDG_CACHE_HOME:-$HOME/.cache}/shell/chpwd-recent-dirs"
    zstyle ':chpwd:*' recent-dirs-pushd true
fi
# ++ }}}

# TODO: English commentary
# ++ Completion Settings {{{
# è£œå®Œä¾¯è£œã‚’ãƒ¡ãƒ‹ãƒ¥ãƒ¼ã‹ã‚‰é¸æŠž
# select=2: è£œå®Œå€™è£œã‚’ä¸€è¦§ã‹ã‚‰é¸æŠž, è£œå®Œå€™è£œãŒ2ã¤ä»¥ä¸Šãªã‘ã‚Œã°ã™ãã«è£œå®Œ
zstyle ':completion:*' menu select=2

# è£œå®Œæ™‚ã«, æ–‡å­—ã®å¤§å°ã‚’åŒºåˆ¥ã—ãªã„
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# è£œå®Œå€™è£œã«ç€è‰²
if [ -n "$LS_COLORS" ]; then
    zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
fi

# ã‚«ãƒ¬ãƒ³ãƒˆãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’éžè¡¨ç¤º
zstyle ':completion:*:cd:*' ignore-parents parent pwd

# ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãƒ•ã‚¡ã‚¤ãƒ«ã¨ã‹ä¸­é–“ãƒ•ã‚¡ã‚¤ãƒ«ã¨ã‹éžè¡¨ç¤º
zstyle ':completion:*:*files' ignored-patterns '*?.o' '*?~' '*\#'

# sudo ã§ã‚‚è£œå®Œã®å¯¾è±¡
zstyle ':completion:*:sudo:*' command-path /usr/local/sbin \
    /usr/local/bin  \
    /usr/sbin       \
    /usr/bin        \
    /sbin           \
    /bin            \
    /usr/X11R6/bin

# è£œå®Œå€™è£œã‚’ã‚­ãƒ£ãƒƒã‚·ãƒ¥
zstyle ':completion:*' use-cache true
zstyle ':completion:*' cache-path "${XDG_CACHE_HOME:-$HOME/.cache}/shell/completion-cache-dirs"

# è©³ç´°æƒ…å ±ã‚’éžè¡¨ç¤º
zstyle ':completion:*' verbose no

# è£œå®Œãƒ•ã‚©ãƒ¼ãƒžãƒƒãƒˆ
zstyle ':completion:*:descriptions' format '%BCompleting%b %U%d%u'

# ã‚»ãƒ‘ãƒ¬ãƒ¼ã‚¿
zstyle ':completion:*' list-separator '-->'
# ++ }}}
# + }}}

# + Aliases {{{
# global
alias -g ...='cd ../..'
alias -g ....='cd ../../..'
alias -g .....='cd ../../../..'
alias -g @g='| grep'
alias -g @l='| less'
alias -g @h='| head'
alias -g @t='| tail'
alias -g @s='| sed'
alias -g @c='| cat'

# suffix
alias -s py=python
alias -s hs=runhaskell
alias -s {png,jpg,bmp,PNG,JPG,BMP}=eog
alias -s {mp3,frac}=audacious
alias -s {html,htm,md}=firefox
function extract() {
    case $1 in
        *.tar.gz|*.tgz) tar xzvf $1;;
        *.tar.xz) tar Jxvf $1;;
        *.zip) unzip $1;;
        *.lzh) lha e $1;;
        *.tar.bz2|*.tbz) tar xjvf $1;;
        *.tar.Z) tar zxvf $1;;
        *.gz) gzip -d $1;;
        *.bz2) bzip2 -dc $1;;
        *.Z) uncompress $1;;
        *.tar) tar xvf $1;;
        *.arj) unarj $1;;
    esac
    return 0
}
alias -s {gz,tgz,zip,lzh,bz2,tbz,Z,tar,arj,xz}=extract

# Don't write to history
alias run-help=' run-help'
alias man=' man'

# Enable color support of ls and also add handy aliases
if [ -x /usr/bin/dircolors ]; then
    case "${OSTYPE}" in
        darwin*)
            alias ls='ls -GF'
            ;;
        linux*)
            if [ -f ~/.dircolors ]; then
                eval "$(dircolors -b ~/.dircolors)" || eval "$(dircolors -b)"
            fi
            alias ls='ls -F --color=auto'
            alias dir='dir --color=auto'
            alias vdir='vdir --color=auto'
            alias grep='grep --color=auto'
            alias fgrep='fgrep --color=auto'
            alias egrep='egrep --color=auto'
            ;;
    esac
fi

# some more ls aliases
alias ll='ls -alF'
alias la='ls -A'
alias lh='ls -lh'
alias l='ls -CF'

# rm aliases
alias rm='rm -i -v'
alias del='rm -rf'

# other aliases
alias df='df -h'
alias ps='ps --sort=start_time'
alias apv='appletviewer'
alias x='exit'
alias g='git'
alias v='vim'
alias relogin='exec $SHELL -l'
alias color256='for c in {000..255}; do echo -n "\e[38;5;${c}m $c" ; [ $(($c%16)) -eq 15 ] && echo;done;echo'

# GoogleDrive aliases
alias gdrive_m='google-drive-ocamlfuse ~/googledrive'
alias gdrive_u='fusermount -u ~/googledrive'

# KeePass alias
alias keepass='gdrive_m | mono ~/KeePass/KeePass.exe &'

# cputmp alias
alias cputmp='cat /sys/class/thermal/thermal_zone0/temp | echo \`cat\`  / 1000  | bc | echo \`cat\` Â°C'
# gputmp alias
alias gputmp='nvidia-settings -q "[gpu:0]/GPUCoreTemp" | grep Attribute  | awk '{print \$4}' | tr -d . | echo \`cat\` Â°C'

# latexmk aliases
alias lmk='latexmk'
alias lmkp='latexmk -pv'
alias lmkc='latexmk -c'

# apt update & upgrade & autoremove alias
alias sudo_update='sudo apt update && sudo apt -y upgrade && sudo apt -y autoremove'
# + }}}

# + Named Directories {{{
function name_dir()
{
    local dir=$1
    local name=$2

    if [ -d $dir ]; then
        hash -d $name=$dir
        return 0
    else
        return 1
    fi
    return 0
}

name_dir ~/.dotfiles/ dotfiles
name_dir ~/workspace/ workspace
name_dir ~/.zsh/ .zsh
name_dir ~/.vim/ .vim
# + }}}

# + Keybind {{{
# Emacs like keybind
bindkey -e

# History search
bindkey '^P' history-beginning-search-backward
bindkey '^N' history-beginning-search-forward
if is-at-least 4.3.10; then
    bindkey '^R' history-incremental-pattern-search-backward
    bindkey '^S' history-incremental-pattern-search-forward
fi
# + }}}
# }}}

# Plugin Settings {{{
# + Plugin Install {{{
# Install zplug/zplug
if [ ! -f $ZPLUG_HOME/init.zsh ]; then
    mkdir -p $ZPLUG_HOME
    git clone https://github.com/zplug/zplug $ZPLUG_HOME
fi
source $ZPLUG_HOME/init.zsh

# Install zsh's other plugin
zplug 'zplug/zplug', hook-build:'zplug --self-manage'
zplug 'zsh-users/zsh-completions'
zplug 'zsh-users/zsh-autosuggestions'
zplug 'supercrabtree/k'
zplug 'zsh-users/zaw'
zplug 'joel-porquet/zsh-dircolors-solarized'
zplug 'zsh-users/zsh-syntax-highlighting', defer:2

# Install plugins if there are plugins that have not been installed
if ! zplug check --verbose; then
    printf "Install? [y/N]: "
    if read -q; then
        echo; zplug install
    fi
fi

# Source plugins and add commands to $PATH
zplug load
# + }}}

# + Plugin Settings{{{
# ++ zplug/zplug
# ++ zsh-users/zsh-completions
# ++ zsh-users/zsh-autosuggestions
# ++ supercrabtree/k

# ++ zsh-users/zaw {{{
if zplug check 'zsh-users/zaw';then
    bindkey '\C-d' zaw-cdr
    bindkey '\C-g' zaw-git-branches
    bindkey '\C-@' zaw-gitdir

    function zaw-src-gitdir () {
        _dir=$(git rev-parse --show-cdup 2>/dev/null)
        if [ $? -eq 0 ]
        then
            candidates=( $(git ls-files ${_dir} | perl -MFile::Basename -nle \
                '$a{dirname $_}++; END{delete $a{"."}; print for sort keys %a}') )
        fi
        actions=("zaw-src-gitdir-cd")
        act_descriptions=("change directory in git repos")
        return 0
    }

    function zaw-src-gitdir-cd () {
        BUFFER="cd $1"
        zle accept-line
        return 0
    }
    zaw-register-src -n gitdir zaw-src-gitdir
fi
# ++ }}}

# ++ joel-porquet/zsh-dircolors-solarized {{{
if zplug check 'joel-porquet/zsh-dircolors-solarized';then
    setupsolarized dircolors.ansi-light
fi
# ++ }}}

# ++ zsh-users/zsh-syntax-highlighting

# + }}}
# }}}

# Settings for zprof
#if (which zprof > /dev/null 2>&1) ;then
#  zprof
#fi


# vim: foldmethod=marker
# vim: foldcolumn=3
# vim: foldlevel=0
