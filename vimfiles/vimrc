
" Initialize {{{
" Don't load when -eval(vim-tiny, vim-small)
if 0 | endif
" Turn off vi compatible mode
if &compatible | set compatible! | endif

" + Encoding {{{
" Required:
set encoding=UTF-8                              " Set encode to utf-8.
scriptencoding utf-8                            " Set character code of Vim script file.(you can use multibyte)
set fileencoding=utf-8                          " 保存時の文字コード
set fileencodings=ucs-boms,utf-8,euc-jp,cp932   " 読み込み時の文字コードの自動判別. 左側が優先される
set fileformats=unix,dos,mac                    " 改行コードの自動判別. 左側が優先される
set ambiwidth=double                            " □や○文字が崩れる問題を解決
" + }}}

" + Filetype {{{
" Required:
"filetype off    "It called in dein#begin.
filetype plugin indent off
" + }}}

" + Autocmd {{{
" Required:
augroup MyAutoCmd
    autocmd!
augroup END
" + }}}

" + Use Python3 {{{
"if has('python3')
"endif
" + }}}

" }}}

" プラグイン機能設定 {{{
" プラグイン管理プラグイン Shougo/dein.vim のインストール
let s:cache_home = empty($XDG_CACHE_HOME) ? expand('~/.vim') : $XDG_CACHE_HOME
let s:dein_dir = s:cache_home . '/dein'
let s:dein_repo_dir = s:dein_dir . '/repos/github.com/Shougo/dein.vim'
if &runtimepath !~# '/dein.vim'
    if !isdirectory(s:dein_repo_dir)
        call system('git clone https://github.com/Shougo/dein.vim ' . shellescape(s:dein_repo_dir))
    endif
    let &runtimepath = s:dein_repo_dir .','. &runtimepath
endif

" プラグイン読み込み＆キャッシュ作成
let s:toml_file = fnamemodify(expand('<sfile>'), ':h').'/dein.toml'
"let s:toml_file = expand('~/.vim').'/dein.toml' " dein.toml が vimrc とは別の場所にある場合
if dein#load_state(s:dein_dir)
    call dein#begin(s:dein_dir)
    call dein#load_toml(s:toml_file)
    call dein#end()
    call dein#save_state()
endif

" 不足プラグインのインストール
if has('vim_starting') && dein#check_install()
    call dein#install()
endif
" }}}

" デフォルト機能設定 {{{
" + Syntax {{{
" Required:
syntax enable
" + }}}

" + ファイル {{{
set hidden                      " ファイル変更中でも他のファイルを開けるようにする
set autoread                    " ファイル内容が変更されると自動読み込みする
set browsedir=buffer            " ファイル保存ダイアログの初期ディレクトリをバッファファイル位置に指定する
set nobackup                    " バックアップしない
"set backupdir=~/.vim/backup    " バックアップディレクトリを指定する
set directory=~/.vim/swp        " スワップファイルディレクトリを指定する
augroup swapchoice-readonly     " スワップファイルを読み込み専用で開く
    autocmd!
    autocmd SwapExists * let v:swapchoice = 'o'
augroup END
" + }}}

" + 表示 {{{
set display=lastline            " @で省略せず, 全て表示
set number                      " 行番号を表示する
set modeline                    " モードラインを有効にする
set modelines=1                 " モードライン行数を設定
set list                        " 不可視文字を表示する
set listchars=tab:»-,trail:.,extends:»,precedes:«,nbsp:% ",eol:↲
augroup highlightZenkakuSpace   " 全角スペースを赤色にする
    autocmd!
    autocmd VimEnter,ColorScheme * highlight ZenkakuSpace term=underline ctermbg=Red guibg=Red
    autocmd VimEnter,WinEnter * match ZenkakuSpace /　/
augroup END
if (v:version == 704 && has("patch785")) || v:version >= 705
    set breakindent             " インデントを考慮した行の折り返し
endif
set cursorline                  " カーソル行をハイライト
augroup cch                     " カレントウィンドウにのみ罫線を引く
    autocmd! cch
    autocmd WinLeave * set nocursorline
    autocmd WinEnter,BufRead * set cursorline
augroup END
highlight clear CursorLine
highlight CursorLine gui=underline
highlight CursorLine ctermbg=black guibg=black
" + }}}

" + 入力 {{{
set cindent                         " 詳細は-> ':help cinoptions-values'
set cinoptions+=:0,l1,g0
set smarttab
set expandtab                       " タブの代わりにスペースを使う
let _curfile=expand("%:r")          " Makefile 編集時は無効
if _curfile == 'Makefile'
    set noexpandtab
endif
set tabstop=4                       " タブの幅
set shiftwidth=4                    " 自動インデントの幅
set softtabstop=0                   " タブを押した時のスペースの量（0ならtabstopの量）
set backspace=indent,eol,start      " バックスペースを押した時の挙動
set whichwrap=b,s,h,l,<,>,[,]       " カーソルを行頭、行末で止まらないようにする
if has('nvim')                      " ヤンクをクリップボードにコピーする
    set clipboard=unnamed
else
    set clipboard=unnamed,autoselect
endif

set showmatch                       " 括弧の対応を表示する（対となる括弧に一瞬飛ぶ）
set matchtime=1                     " 括弧の対応を表示する時間
set completeopt=menuone             " 補完候補が1つでも表示
set pumheight=10                    " 補完メニューの高さ
set wildmenu                        " 入力コマンドの補完
set wildmode=longest:full,full      " 入力コマンドの補完オプション
set history=1000                    " 入力コマンドの履歴数

" ++ マウス {{{
if has('mouse')
    set mouse=a
    if has('nvim')
    elseif has('mouse_sgr')
        set ttymouse=sgr
    elseif v:version > 703 || v:version is 703 && has('patch632')
        set ttymouse=sgr
    else
        set ttymouse=xterm2
    endif
endif
" ++ }}}

" ++ クリップボード {{{
if &term =~ "xterm"
    let &t_SI .= "\e[?2004h"
    let &t_EI .= "\e[?2004l"
    let &pastetoggle = "\e[201~"

    function XTermPasteBegin(ret)
        set paste
        return a:ret
    endfunction

    inoremap <special> <expr> <Esc>[200~ XTermPasteBegin("")
endif
" ++ }}}
" + }}}

" + 検索 "{{{
set noincsearch " インクリメンタルサーチを行わない
set hlsearch    " 検索結果をハイライトする
set ignorecase  " 検索時に文字の大小を区別しない
set smartcase   " 検索時に大文字を含んでいたら大小を区別する
set nowrapscan  " 検索をファイルの先頭へループしない
set magic       " 正規表現使用時に magic モードにする
" + }}}

" + キーマッピング "{{{
"-----------------------------------------------------------------------------------------------"
"     command     |  normal  | insert |  command line  | visual | select | waiting computation |
" map  / noremap  |    @     |        |                |   @    |   @    |          @          |
" nmap / nnoremap |    @     |        |                |        |        |                     |
" vmap / vnoremap |          |        |                |   @    |   @    |                     |
" omap / onoremap |          |        |                |        |        |          @          |
" xmap / xnoremap |          |        |                |   @    |        |                     |
" smap / snoremap |          |        |                |        |   @    |                     |
" map! / noremap! |          |   @    |       @        |        |        |                     |
" imap / inoremap |          |   @    |                |        |        |                     |
" cmap / cnoremap |          |        |       @        |        |        |                     |
"-----------------------------------------------------------------------------------------------"

" <C-l>でESCする
nnoremap <C-l> <ESC>
vnoremap <C-l> <ESC>
inoremap <C-l> <ESC>
cnoremap <C-l> <ESC>

" コロンとセミコロン入れ替え
noremap : ;
noremap ; :

" 保存/終了をスペースで
nnoremap <Space>w  :<C-u>w<CR>
nnoremap <Space>W  :<C-u>wa<CR>
nnoremap <Space>q  :<C-u>q<CR>
"nnoremap <Space>Q  :<C-u>q!<CR>

" カーソルを表示行で移動する
nnoremap j gj
nnoremap k gk
nnoremap gj j
nnoremap gk k
nnoremap <Down> gj
nnoremap <Up>   gk

" 行末までのヤンクにする
nnoremap Y y$

" インデント
nnoremap <silent> == gg=G''

" CTRL-hjklでウィンドウ間のカーソル移動
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l
nnoremap <C-h> <C-w>h

" バッファの移動
nnoremap <C-p> :<C-u>bp<CR>
nnoremap <C-n> :<C-u>bn<CR>

" 行頭までカーソル移動
nnoremap <S-h> ^

" 行末までカーソル移動
nnoremap <S-l> $

" インクリメント
nnoremap + <C-a>
" デクリメント
nnoremap - <C-x>

" Escの2回押しでハイライト消去
nnoremap <silent> <C-l><C-l> :<C-u>noh<CR>

" 保存して閉じる, 保存せず閉じるを無効
nnoremap ZZ <Nop>
nnoremap ZQ <Nop>

" EXモード無効
nnoremap Q <Nop>

" 入力中カーソルを移動
inoremap <C-f> <right>
inoremap <C-b> <left>

" 隣接した{}で改行したらインデント
inoremap {<CR> {}<Left><CR><Esc><S-o>
inoremap [<CR> []<Left><CR><Esc><S-o>
inoremap (<CR> ()<Left><CR><Esc><S-o>

" + }}}
" }}}

" Finalize "{{{
" + Filetype "{{{
" Required:
filetype on
filetype plugin indent on 
" + }}}
" }}}


" vim: fdm=marker fdc=3 fdl=0
